plugins {
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.asciidoctor.convert" version "1.5.10"
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

group = 'com.woowacourse'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    //spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    //database
    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'com.h2database:h2'
    //flyway
    implementation 'org.flywaydb:flyway-core:6.4.2'

    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //restdocs
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    //jwt
    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    //svg
    implementation 'org.apache.xmlgraphics:batik-all:1.12'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.4'
    implementation 'xml-apis:xml-apis:1.4.01'
    implementation 'xml-apis:xml-apis-ext:1.3.04'

    //cryptor
    implementation 'commons-codec:commons-codec:1.15'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }

    finalizedBy jacocoTestReport
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

task createDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/asciidoc/html5/index.html")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn createDocument
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

jacocoTestReport {
    dependsOn test
    reports{
        html.enabled true
        xml.enabled true
        csv.enabled false
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'BUNDLE'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.79
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.78
            }
        }
    }
}

project.tasks["jacocoTestCoverageVerification"].finalizedBy "sonarqube"
sonarqube {
    def sonarProperties = new Properties()
    sonarProperties.load(new FileInputStream(file("src/main/resources/config/sonar.properties")))
    def sonarToken = sonarProperties.getProperty("SONAR_TOKEN")

    properties {
        property "sonar.host.url", "http://zzimkkong-service.o-r.kr:8000"
        property "sonar.login", sonarToken
        property 'sonar.sources', 'src'
        property 'sonar.language', 'java'
        property 'sonar.projectVersion', '0.0.1-SNAPSHOT'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.java.binaries', 'build/classes'
        property 'sonar.test.inclusions', '**/*Test.java'
        property 'sonar.exclusions', '**/*Doc*.java, **/resources/**'
    }
}
